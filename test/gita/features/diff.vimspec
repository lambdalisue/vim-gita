Describe gita#features#diff
  Before
    Init
    edit README.md
    let gita = gita#get()
    let gita.git.cache.repository.clear()
    let gita.operations._previous_args = []
    let gita.operations._previous_args_show = []
    function! gita.operations.diff(...) abort
      let self._previous_args = a:000
      return {
            \ 'status': 0,
            \ 'stdout': 'CONTENTS',
            \}
    endfunction
    function! gita.operations.show(...) abort
      let self._previous_args_show = a:000
      return {
            \ 'status': 0,
            \ 'stdout': 'CONTENTS',
            \}
    endfunction
    function! gita.operations.merge_base(...) abort
      return {
            \ 'status': 0,
            \ 'stdout': 'COMMON-ANCESTOR',
            \}
    endfunction
  End

  Context #split_commit({commit}[, {options}])
    It should split commit1...commit2 to commit1...commit2 and commit2
      let ret = gita#features#diff#split_commit('commit1...commit2')
      Assert Equals(ret[0], 'commit1...commit2')
      Assert Equals(ret[1], 'commit2')
    End

    It should split commit1..commit2 to commit1 and commit2
      let ret = gita#features#diff#split_commit('commit1..commit2')
      Assert Equals(ret[0], 'commit1')
      Assert Equals(ret[1], 'commit2')
    End

    It should split commit1 to WORKTREE and commit1
      let ret = gita#features#diff#split_commit('commit1')
      Assert Equals(ret[0], 'WORKTREE')
      Assert Equals(ret[1], 'commit1')
    End

    It should split commit1 to INDEX and commit1 if cached is specified
      let ret = gita#features#diff#split_commit('commit1', {
            \ 'cached': 1,
            \})
      Assert Equals(ret[0], 'INDEX')
      Assert Equals(ret[1], 'commit1')
    End
  End

  Context #exec([{options}, {config}])
    It should call gita.operations.diff internally
      call gita#features#diff#exec({
            \ '--': [],
            \ 'ignore-submodules': 1,
            \ 'no-prefix': 1,
            \ 'no-color': 1,
            \ 'unified': 1,
            \ 'histogram': 1,
            \ 'cached': 1,
            \ 'commit': 'HEAD',
            \ 'name-status': 1,
            \})
      Assert Equals(gita.operations._previous_args, [{
            \ '--': [],
            \ 'ignore-submodules': 1,
            \ 'no-prefix': 1,
            \ 'no-color': 1,
            \ 'unified': 1,
            \ 'histogram': 1,
            \ 'cached': 1,
            \ 'commit': 'HEAD',
            \ 'name-status': 1,
            \}, {}])
    End

    It should remove unknown options internally
      call gita#features#diff#exec({
            \ 'commit': 'HEAD',
            \ 'unknown_option': 1,
            \})
      Assert Equals(gita.operations._previous_args, [{
            \ 'commit': 'HEAD',
            \}, {}])
    End
  End

  Context #exec_cached([{options}, {config}])
    It should NOT call gita.operations.diff for 2nd call with same options
      call gita#features#diff#exec_cached({
            \ 'commit': 'HEAD',
            \ 'ignore-submodules': 1,
            \})
      let gita.operations._previous_args = []
      call gita#features#diff#exec_cached({
            \ 'commit': 'HEAD',
            \ 'ignore-submodules': 1,
            \})
      Assert Equals(gita.operations._previous_args, [])
    End

    It should call gita.operations.diff for 2nd call with different options
      call gita#features#diff#exec_cached({
            \ 'commit': 'HEAD',
            \ 'ignore-submodules': 1,
            \})
      let gita.operations._previous_args = []
      call gita#features#diff#exec_cached({
            \ 'commit': 'HEAD',
            \ 'ignore-submodules': 0,
            \})
      Assert Equals(gita.operations._previous_args, [{
            \ 'commit': 'HEAD',
            \ 'ignore-submodules': 0,
            \}, {}])
    End
  End

  Context #show([{options}, {config}])
    It should open a single diff buffer for entire repository if no filename is specified
      call gita#features#diff#show({
            \ 'split': 0,
            \ 'commit': 'HEAD',
            \})
      " it should execute gita.operations.diff internally
      Assert Equals(gita.operations._previous_args, [{
            \ 'no-color': 1,
            \ 'commit': 'HEAD',
            \}, { 'echo': 'fail' }])
      " check the buffer
      Assert Equals(bufname('%'), gita#utils#buffer#bufname('HEAD', 'diff'))
      Assert Equals(getline(0, '$'), ['CONTENTS'])
      Assert Equals(&l:buftype, 'nofile')
      Assert Equals(&l:modifiable, 0)
      Assert Equals(&l:readonly, 1)
      Assert Equals(&l:filetype, 'diff')
      Assert Equals(gita#meta#get('filename'), '')
      Assert Equals(gita#meta#get('commit'), 'HEAD')
    End

    It should open a unified diff for a particular file if filename is specified with split=0
      call gita#features#diff#show({
            \ '--': ['README.md'],
            \ 'commit': 'HEAD',
            \ 'split': 0,
            \})
      " it should execute gita.operations.diff internally
      " Note: git diff requires UNIX path
      Assert Equals(gita.operations._previous_args, [{
            \ '--': gita#utils#path#unix_abspath(['README.md']),
            \ 'no-color': 1,
            \ 'commit': 'HEAD',
            \}, { 'echo': 'fail' }])
      " check the buffer
      Assert Equals(bufname('%'), gita#utils#buffer#bufname('HEAD', 'README.md.diff'))
      Assert Equals(getline(0, '$'), ['CONTENTS'])
      Assert Equals(&l:buftype, 'nofile')
      Assert Equals(&l:modifiable, 0)
      Assert Equals(&l:readonly, 1)
      Assert Equals(&l:filetype, 'diff')
      Assert Equals(gita#meta#get('filename'), gita#utils#path#unix_abspath('README.md'))
      Assert Equals(gita#meta#get('commit'), 'HEAD')
    End

    It should open two buffers for the current buffer if no filename is specified with split=1
      call gita#features#diff#show({
            \ 'commit': 'HEAD',
            \ 'split': 1,
            \})
      " it should NOT execute gita.operations.diff internally
      Assert Equals(gita.operations._previous_args, [])
      " but it should execute gita.operations.show internally
      Assert Equals(gita.operations._previous_args_show, [{
            \ 'object': 'HEAD:README.md',
            \}, { 'echo': '' }])
      " check the buffer (COMMIT1)
      Assert Equals(
            \ bufname('%'),
            \ gita#utils#buffer#bufname('README.md'),
            \)
      Assert Equals(getline(0, '$'), readfile('README.md'))
      Assert Equals(&l:buftype, '')
      Assert Equals(&l:modifiable, 1)
      Assert Equals(&l:readonly, 0)
      Assert Equals(gita#meta#get('filename'), '')
      Assert Equals(gita#meta#get('commit'), '')

      " check the buffer (COMMIT2)
      wincmd p
      Assert Equals(
            \ bufname('%'),
            \ gita#utils#buffer#bufname('HEAD', 'README.md'),
            \)
      Assert Equals(getline(0, '$'), ['CONTENTS'])
      Assert Equals(&l:buftype, 'nofile')
      Assert Equals(&l:modifiable, 0)
      Assert Equals(&l:readonly, 1)
      Assert Equals(
            \ gita#meta#get('filename'),
            \ gita#utils#path#unix_abspath('README.md'),
            \)
      Assert Equals(gita#meta#get('commit'), 'HEAD')
    End

    It should open two buffers for the specified file if split=1
      call gita#features#diff#show({
            \ '--': ['README.md'],
            \ 'commit': 'HEAD',
            \ 'split': 1,
            \})
      " it should NOT execute gita.operations.diff internally
      Assert Equals(gita.operations._previous_args, [])
      " but it should execute gita.operations.show internally
      Assert Equals(gita.operations._previous_args_show, [{
            \ 'object': 'HEAD:README.md',
            \}, { 'echo': '' }])
      " check the 1st buffer (COMMIT1)
      Assert Equals(
            \ bufname('%'),
            \ gita#utils#buffer#bufname('README.md'),
            \)
      Assert Equals(
            \ getline(0, '$'),
            \ readfile('README.md'),
            \)
      Assert Equals(&l:buftype, '')
      Assert Equals(&l:bufhidden, '')
      Assert Equals(&l:modifiable, 1)
      Assert Equals(&l:readonly, 0)
      Assert Equals(gita#meta#get('filename'), '')
      Assert Equals(gita#meta#get('commit'), '')

      " check the 2nd buffer (COMMIT2)
      wincmd p
      Assert Equals(
            \ bufname('%'),
            \ gita#utils#buffer#bufname('HEAD', 'README.md'),
            \)
      Assert Equals(getline(0, '$'), ['CONTENTS'])
      Assert Equals(&l:buftype, 'nofile')
      Assert Equals(&l:modifiable, 0)
      Assert Equals(&l:readonly, 1)
      Assert Equals(
            \ gita#meta#get('filename'),
            \ gita#utils#path#unix_abspath('README.md'),
            \)
      Assert Equals(gita#meta#get('commit'), 'HEAD')

    End
  End
End
