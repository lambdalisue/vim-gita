Describe gita#features#browse
  Before
    let prefix = 'https://github.com/lambdalisue/vim-gita/blob/.*'
    let gita = gita#get()
    let operations_saved = deepcopy(gita.operations)

    function! gita.operations.browse(...) abort
      let self._previous_args = a:000
    endfunction
    let gita.operations._previous_args = []
  End
  After
    let gita.operations = operations_saved
  End

  Context #exec([{options}, {config}])
    It should return urls of specifield files
      let ret = gita#features#browse#exec({
            \ '--': ['README.md', 'LICENSE.md'],
            \})
      Assert Equals(ret.status, 0)
      Assert Match(ret.urls[0], printf('%s/README.md', prefix))
      Assert Match(ret.urls[1], printf('%s/LICENSE.md', prefix))
    End
  End

  Context #open([{options}, {config}])
    It should open system browser to show urls of specifield files
      Assert True(exists('*gita#features#browse#open'))
      Skip It is not possible to test this behavior
    End
  End

  Context #yank([{options}, {config}])
    It should yank the last url of the specified files
      call gita#features#browse#yank({
            \ '--': ['README.md', 'LICENSE.md'],
            \})
      Assert Match(@", printf('%s/LICENSE.md', prefix))
    End

    if has('clipboard')
      It should yank the last url of the specified files to clipboard as well
        call gita#features#browse#yank({
              \ '--': ['README.md', 'LICENSE.md'],
              \})
        Assert Match(
              \ getreg(v:register),
              \ printf('%s/LICENSE.md', prefix)
              \)
      End
    endif
  End

  Context #echo([{options}, {config}])
    It should echo urls of the specified files
      redir => contents
      call gita#features#browse#echo({
            \ '--': ['README.md', 'LICENSE.md'],
            \})
      redir END
      let lines = split(contents, "\n")
      Assert Match(lines[0], printf('%s/README.md', prefix))
      Assert Match(lines[1], printf('%s/LICENSE.md', prefix))
    End
  End

  Context #command({bang}, {range}[, {cmdline}])
    It should automatically call a correct method
      Assert True(exists('*gita#features#browse#command'))
      Skip It is difficult to test this behavior
    End
  End

  Context #complete({arglead}, {cmdline}, {cursorpos})
    It should call ArgumentParser.complete function
      Assert True(exists('*gita#features#browse#complete'))
      Skip It is not possible to access script local variable
    End
  End

  Context #translate_url({url}, {translate_patterns}, {options})
    It should be able to translate Github (https://github.com/) urls
      let patterns = [
            \ 'https://github.com/%s/%s',
            \ 'git://github.com/%s/%s',
            \ 'git@github.com:%s/%s',
            \ 'ssh://git@github.com/%s/%s',
            \ 'https://github.com/%s/%s.git',
            \ 'git://github.com/%s/%s.git',
            \ 'git@github.com:%s/%s.git',
            \ 'ssh://git@github.com/%s/%s.git',
            \]
      let pairs = [
            \ ['lambdalisue', 'vim-gita'],
            \ ['vim-jp', 'vital.vim'],
            \]
      for pattern in patterns
        for [user, repo] in pairs
          let url = gita#features#browse#translate_url(
                \ printf(pattern, user, repo),
                \ g:gita#features#browse#translation_patterns,
                \ {},
                \)
          Assert Match(
                \ url,
                \ printf('^https://github.com/%s/%s/blob', user, repo),
                \)
        endfor
      endfor
    End

    It should be able to translate bitbucket (https://bitbucket.org/) urls
      let patterns = [
            \ 'https://bitbucket.org/%s/%s',
            \ 'git://bitbucket.org/%s/%s',
            \ 'git@bitbucket.org:%s/%s',
            \ 'ssh://git@bitbucket.org/%s/%s',
            \ 'https://bitbucket.org/%s/%s.git',
            \ 'git://bitbucket.org/%s/%s.git',
            \ 'git@bitbucket.org:%s/%s.git',
            \ 'ssh://git@bitbucket.org/%s/%s.git',
            \]
      let pairs = [
            \ ['lambdalisue', 'vim-gita'],
            \ ['vim-jp', 'vital.vim'],
            \]
      for pattern in patterns
        for [user, repo] in pairs
          let url = gita#features#browse#translate_url(
                \ printf(pattern, user, repo),
                \ g:gita#features#browse#translation_patterns,
                \ {},
                \)
          Assert Match(
                \ url,
                \ printf('^https://bitbucket.org/%s/%s/src', user, repo),
                \)
        endfor
      endfor
    End
  End
End

